buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        maven { url "${mavenRepository}/maven-public"}
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven { url "${mavenRepository}/maven-public"}
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Brussels-SR2'
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.SR1'
        }
    }

    dependencies {
        compile 'org.springframework:spring-web'
        compile 'com.fasterxml.jackson.core:jackson-databind'
        compile 'org.apache.commons:commons-lang3'
        compileOnly 'org.projectlombok:lombok'
    }
}

configure(subprojects.findAll {it.name.endsWith("-boot")}) {
    dependencies {
        compile 'org.springframework.boot:spring-boot-devtools'
        compile 'org.springframework.boot:spring-boot-configuration-processor'

        compile project(":common")

        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.cloud:spring-cloud-starter-hystrix'
        compile 'org.springframework.cloud:spring-cloud-starter-feign'

    }
}